require Math;

object KrakenLookAtClip : KrakenClip {
  Index matrixIndex;
  KrakenClip childClip;
  String names[];
  SInt32 controlId[];
  UInt32 nameToId[String];
  Vec3 targets[];
  Vec3 axes[];
  Boolean locked[];
  Float32 amounts[];

  UInt64 rigVersion;
  KrakenClipLookup lookup;
};

function KrakenLookAtClip(Index matrixIndex, KrakenClip childClip) {
  this.matrixIndex = matrixIndex;
  this.childClip = childClip;
  this.lookup = null;
  this.rigVersion = 0;
}

/// \dfgPresetDefault target {"x": 1.0, "y": 0.0, "z": 0.0}
/// \dfgPresetDefault axis {"x": 0.0, "y": 1.0, "z": 0.0}
/// \dfgPresetDefault locked true
/// \dfgPresetDefault amount 1.0
/// \dfgPresetRange amount (0.0, 1.0)
function KrakenLookAtClip.defineLookAt!(
  String name, 
  Vec3 target,
  Vec3 axis,
  Boolean locked,
  Float32 amount
) {
  UInt32 id = this.nameToId.get(name, this.names.size());
  if(id == this.names.size()) {
    this.nameToId[name] = this.names.size();
    this.names.push(name);
    this.controlId.push(-1);
    this.targets.push(target);
    this.axes.push(axis);
    this.locked.push(locked);
    this.amounts.push(amount);
  } else {
    this.targets[id] = target;
    this.axes[id] = axis;
    this.locked[id] = locked;
    this.amounts[id] = amount;
  }
  this.lookup = null;
}

UInt32 KrakenLookAtClip.getChannelCount() {
  return this.names.size();
}

String KrakenLookAtClip.getChannelName(UInt32 id) {
  return this.names[id];
}

UInt32 KrakenLookAtClip.getChannelType(UInt32 id) {
  return KrakenClipChannel_Xfo;
}

Float32 KrakenLookAtClip.evaluateFloat32!(UInt32 id, io KrakenKLRig rig, KrakenClipContext context) {
  return Float32();
}

Xfo KrakenLookAtClip.evaluateXfo!(UInt32 id, io KrakenKLRig rig, KrakenClipContext context) {

  if(rig == null)
    return Xfo();
  if(this.targets[id].almostEqual(Vec3()))
    return Xfo();

  KrakenControl controls<> = rig.getControls();
  if(this.controlId[id] == -1) {
    for(Size i=0;i<controls.size();i++) {
      if(controls[i].name == this.names[id]) {
        this.controlId[id] = i;
        break;
      }
    }
    if(this.controlId[id] == -1)
      return Xfo(); // not found
  }

  Xfo global = controls[this.controlId[id]].global;
  Xfo parentSpace = controls[this.controlId[id]].global * controls[this.controlId[id]].local.inverse();

  Vec3 srcDir = parentSpace.ori.rotateVector(this.targets[id]).unit();
  Vec3 newDir = (context.matrices[this.matrixIndex].translation() - parentSpace.tr).unit();
  if(newDir.almostEqual(Vec3()))
    return Xfo();

  Quat result;
  if(this.locked[id] && !this.axes[id].almostEqual(Vec3())) {

    Vec3 axis = parentSpace.ori.rotateVector(this.axes[id]);
    srcDir = (srcDir - (axis.dot(srcDir) * axis)).unit();
    newDir = (newDir - (axis.dot(newDir) * axis)).unit();

  }

  Quat offsetOri;
  offsetOri.setFrom2Vectors(srcDir, newDir);
  result = parentSpace.ori * offsetOri;

  global.ori = parentSpace.ori.sphericalLinearInterpolate(result, Float32(Math_clamp(this.amounts[id], 0.0, 1.0)));

  return parentSpace.inverse() * global;
}

function KrakenLookAtClip.apply(io KrakenKLRig rig, KrakenClipContext context, Float32 factor) {
  if(this.childClip)
    this.childClip.apply(rig, context, factor);

  KrakenLookAtClip clip = this;
  if(clip.lookup == null)
    clip.lookup = KrakenClipLookup(rig, clip);
  clip.lookup.applyClip(rig, clip, context, factor);
}

function JSONDictValue KrakenLookAtClip.saveToJson!() {
  JSONArrayValue lookats();
  for(Size i=0;i<this.names.size();i++) {
    JSONDictValue lookat();
    lookat.setString('name', this.names[i]);
    lookat.setVec3('target', this.targets[i]);
    lookat.setVec3('axis', this.axes[i]);
    lookat.setBoolean('locked', this.locked[i]);
    lookat.setScalar('amount', this.amounts[i]);
    lookats.add(lookat);
  }
  JSONDictValue content();
  content.setInteger('matrixIndex', this.matrixIndex);
  if(this.childClip) {
    content.setString('childClipType', this.childClip.type());
    content.set('childClip', this.childClip.saveToJson());
  }
  content.set('lookats', lookats);
  return content;
}

function KrakenLookAtClip.loadFromJson!(JSONDictValue json) {
  this.names.resize(0);
  this.nameToId.clear();
  this.targets.resize(0);
  this.amounts.resize(0);
  this.rigVersion = 0;

  this.matrixIndex = json.getInteger('matrixIndex');
  if(json.has('childClipType')) {
    String childClipType = json.getString('childClipType');
    this.childClip = KrakenClip_getFromTypeName(childClipType);
    this.childClip.loadFromJson(json.get('childClip'));
  } else {
    this.childClip = null;
  }

  JSONArrayValue lookats = json.get('lookats');
  for(Size i=0;i<lookats.size();i++) {
    JSONDictValue lookat = lookats.get(i);
    String name = lookat.getString('name');
    Vec3 target = lookat.getVec3('target');
    Vec3 axis = lookat.getVec3('axis');
    Boolean locked = lookat.getBoolean('locked');
    Float32 amount = lookat.getScalar('amount');
    this.defineLookAt(name, target, axis, locked, amount);
  }
}
