/// \dfgPresetOmit
object KrakenClipLookup {
  private SInt32 channelToControl[];
  private SInt32 channelToScalarAttribute[];
};

/// \dfgPresetOmit
function KrakenClipLookup(Ref<KrakenKLRig> rig, Ref<KrakenClip> clip) {

  SInt32 controlToId[String];
  KrakenControl controls<> = rig.getControls();
  for(Size i=0;i<controls.size();i++)
    controlToId[controls[i].name] = i;

  SInt32 scalarToId[String];
  KrakenScalarAttribute scalarAttributes<> = rig.getScalarAttributes();
  for(Size i=0;i<scalarAttributes.size();i++)
    scalarToId[scalarAttributes[i].name] = i;

  this.channelToControl.resize(clip.getChannelCount());
  this.channelToScalarAttribute.resize(clip.getChannelCount());
  
  for(Size i=0;i<clip.getChannelCount();i++) {
    String name = clip.getChannelName(i);
    this.channelToControl[i] = controlToId.get(name, -1);
    this.channelToScalarAttribute[i] = scalarToId.get(name, -1);
  }
}

/// \dfgPresetOmit
function KrakenClipLookup.applyClip(io KrakenKLRig rig, Ref<KrakenClip> clipRef, KrakenClipContext context, Float32 factor) {
  KrakenClip clip = clipRef;
  KrakenControl controls<> = rig.getControls();
  KrakenScalarAttribute scalarAttributes<> = rig.getScalarAttributes();

  for(Size i=0;i<this.channelToControl.size();i++) {
    if(this.channelToControl[i] != -1) {
      Xfo xfo = controls[this.channelToControl[i]].local;
      xfo = xfo.linearInterpolate(clip.evaluateXfo(i, context), factor);
      controls[this.channelToControl[i]].local = xfo.toMat44();
    }
    if(this.channelToScalarAttribute[i] != -1) {
      Float32 value = scalarAttributes[this.channelToScalarAttribute[i]].value;
      value = Math_linearInterpolate(value, clip.evaluateFloat32(i, context), factor);
      scalarAttributes[this.channelToScalarAttribute[i]].value = value;
    }
  }
}
