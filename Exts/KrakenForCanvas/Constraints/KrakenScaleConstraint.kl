require Kraken;

struct KrakenScaleConstraint : KrakenConstraint {
};

/// \dfgPresetFolder Constraints
function KrakenScaleConstraint(
  Xfo offset,
) {
  this.offset = offset;
}

/// \dfgPresetFolder Constraints
function KrakenScaleConstraint.addConstrainer!(
  Xfo constrainer
) {
  this.constrainers.push(constrainer);
}

/// \dfgPresetFolder Constraints
function Xfo KrakenScaleConstraint.compute?(
  Xfo xfo
) {
  Xfo result = xfo;
  result.sc = Vec3();

  for(Size i=0;i<this.constrainers.size();i++) {
    result.sc = result.sc.add(this.constrainers[i].sc);
  }

  result.sc *= 1.0 / Float32(this.constrainers.size());

  return result.toMat44() * this.offset.toMat44();
}

/// \dfgPresetFolder Constraints
function Xfo computeKrakenScaleConstraint(Xfo offset, Xfo constrainer, Xfo constrainee) {
  KrakenScaleConstraint constraint(offset);
  constraint.addConstrainer(constrainer);
  return constraint.compute(constrainee);
}

/// \dfgPresetFolder Constraints
function Xfo KrakenScaleConstraint.computeOffset?(
  Xfo constrainee
) {

  KrakenScaleConstraint tmpConstraint(Xfo());
  tmpConstraint.constrainers = this.constrainers;

  Mat44 global = tmpConstraint.compute(constrainee).toMat44();
  Xfo result = global.inverse() * constrainee.toMat44();
  result.tr = Vec3(0.0);
  result.ori = Quat();

  return result;
}

/// \dfgPresetFolder Constraints
function Xfo KrakenScaleConstraint.computeOffsetSimple!(Xfo constrainee, Xfo constrainer) {
  this.offset = Xfo();
  this.constrainers.resize(1);
  this.constrainers[0] = constrainer;
  return this.computeOffset(constrainee);
}
